apply plugin: 'com.android.application'


android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"
    defaultConfig {
        applicationId "com.c3.jbz"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField("String", "KEY_USERID", "\"KEY_USERID\"")//用户id
        buildConfigField("String", "KEY_WX_APPID", "\"KEY_WX_APPID\"")//微信appid
        buildConfigField("String", "KEY_ALI_APPID", "\"KEY_ALI_APPID\"")//支付宝appid
        buildConfigField("String", "SUFFIX_USER_AGENT", "\";JBZBrowser\"")//浏览器的user-agent后缀
        buildConfigField("String", "WEB_JS_NAME_ISTOP", "\"javascript:isTopLevelPage()\"")
        buildConfigField("String", "WEB_JS_NAME_goShare", "\"javascript:goShare()\"")
        buildConfigField("String", "WEB_JS_NAME_handleWXRespEvent", "\"javascript:handleWXRespEvent(%d,%d,'%s')\"")
        buildConfigField("String", "WEB_JS_NAME_handleALIRespEvent", "\"javascript:handleALIRespEvent('%s')\"")

        buildConfigField("String", "KEY_MSG_TYPE", "\"KEY_MSG_TYPE\"")//消息类型key
        buildConfigField("int", "MSG_TYPE_NORMAL", "0")//消息类型，一般消息
        buildConfigField("int", "MSG_TYPE_NOTICE", "1")//消息类型，通知
        buildConfigField("int", "MSG_TYPE_LOGISTICS", "2")//消息类型，物流
        ndk {
            //选择要添加的对应cpu类型的.so库。
            abiFilters 'armeabi', 'armeabi-v7a', 'arm64-v8a'
            // 还可以添加 'x86', 'x86_64', 'mips', 'mips64'
        }
    }

    lintOptions {
        abortOnError false
    }

    //签名配置
    signingConfigs {

        //签名jubaozan
        sign_jubaozan {
            keyAlias 'jubaozan'//别名
            keyPassword 'jubaozan20171002'//别名密码
            storeFile file('jubaozan.keystore')//签名文件路径
            storePassword 'jubaozan20171002'//签名密码
        }
        //签名2xbj
        sign_xbj {
            keyAlias 'xbj'//别名
            keyPassword 'xbj20171211'//别名密码
            storeFile file('xbj.jks')//签名文件路径
            storePassword 'xbj20171211'//签名密码
        }

        //...你可以配置多个

    }

    //多包名设置
    productFlavors {
        //https://wx.jubaozan.cn/client/start.do?deviceType=1&deviceId=%s&siteId=1123&versionCode=${defaultConfig.versionCode} 测试环境地址
        //\"file:///android_asset/login.html?deviceType=1&deviceId=%s&siteId=123&versionCode=${defaultConfig.versionCode}\"
//        https://m.c3tech.cn/client/start.do?deviceType=1&deviceId=%s&siteId=1263&versionCode=${defaultConfig.versionCode}
        flavor_jubaozan {
            applicationId = "com.c3.jbz"
            manifestPlaceholders.put("pkgname", applicationId)
            resValue("string", "app_name", "全心官方商城")//应用名
            buildConfigField("String", "mainUrl",
                    "\"https://m.c3tech.cn/client/start.do?deviceType=1&deviceId=%s&siteId=1263&versionCode=${defaultConfig.versionCode}\"")
//h5页面首页
            signingConfig signingConfigs.sign_jubaozan

            manifestPlaceholders = [
                    JPUSH_PKGNAME : applicationId,
                    JPUSH_APPKEY : "cfbf471c8733cb8788164ec8", //JPush上注册的包名对应的appkey.
                    JPUSH_CHANNEL : "developer-default" //暂时填写默认值即可.
            ]
        }

        //https://wx.jubaozan.cn/client/start.do?deviceType=1&deviceId=%s&siteId=1473&versionCode=${defaultConfig.versionCode} 测试环境地址
        //https://m.c3tech.cn/client/start.do?deviceType=1&deviceId=%s&siteId=1473&versionCode=${defaultConfig.versionCode} 正式环境
        flavor_xbj {
            applicationId = "com.c3.xbj"
            manifestPlaceholders.put("pkgname", applicationId)
            resValue("string", "app_name", "熊本家")//应用名
            buildConfigField("String", "mainUrl",
                    "\"https://wx.jubaozan.cn/client/start.do?deviceType=1&deviceId=%s&siteId=1473&versionCode=${defaultConfig.versionCode}\"")
//h5页面首页
            signingConfig signingConfigs.sign_xbj

            manifestPlaceholders = [
                    JPUSH_PKGNAME : applicationId,
                    JPUSH_APPKEY : "9f51fd8fc93078dd43d65cad", //JPush上注册的包名对应的appkey.
                    JPUSH_CHANNEL : "developer-default" //暂时填写默认值即可.
            ]
        }

    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

//解决执行mockableAndroidJar失败的问题
gradle.taskGraph.whenReady {
    tasks.each { task ->
        if (task.name.equals('mockableAndroidJar')) {
            task.enabled = false
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:design:26.0.0-alpha1'
    compile 'com.android.support:recyclerview-v7:26.1.0'
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    // Plain MVP
    // or
    // MVP + ViewState support


    compile 'com.jakewharton:butterknife:' + rootProject.ext.butterknifeVersion
    annotationProcessor 'com.jakewharton:butterknife-compiler:' + rootProject.ext.butterknifeVersion

    //wm sdk
    compile 'com.android.support:appcompat-v7:26.0.0-alpha1'
    compile 'com.liulishuo.filedownloader:library:1.6.4'
    compile 'com.hannesdorfmann.mosby3:mvp:3.0.4'
    compile 'com.android.support:support-v4:26.+'
    compile 'com.tencent.mm.opensdk:wechat-sdk-android-with-mta:+'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    testCompile 'junit:junit:4.12'

    //极光推送
    compile 'cn.jiguang.sdk:jpush:3.1.1'
    compile 'cn.jiguang.sdk:jcore:1.1.9'

    compile "android.arch.lifecycle:extensions:${rootProject.ext.arch_version}"
    compile "android.arch.persistence.room:runtime:${rootProject.ext.arch_version}"
    annotationProcessor "android.arch.lifecycle:compiler:${rootProject.ext.arch_version}"
    annotationProcessor "android.arch.persistence.room:compiler:${rootProject.ext.arch_version}"

    testCompile("android.arch.core:core-testing:${arch_version}", {
        exclude group: 'com.android.support', module: 'support-compat'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-core-utils'
        exclude group: 'com.android.support', module: 'support-core-utils'
    })

    //类似java8 中的时间日期库
    compile 'com.jakewharton.threetenabp:threetenabp:1.0.5'
    compile 'com.jakewharton.timber:timber:4.5.1'
}
